# Maintainer: Daichi Shinozaki <dseg@shield.jp>
# Contributor: Jean-Sébastien Ney <jeansebastien.ney@gmail.com>
# Contributor: James Cleveland <jc@blackflags.co.uk>
# Contributor: Eimantas Bunevičius <eimantaster@gmail.com>

_cfgdir=/etc/nginx # Prologin-SADM
pkgname=openresty
pkgver=1.11.2.2
pkgrel=1
pkgdesc="A Fast and Scalable Web Platform by Extending NGINX with Lua"
arch=('i686' 'x86_64')
url="http://openresty.org/"
license=('BSD')
depends=('perl>=5.6.1' 'readline' 'pcre' 'openssl')
source=(https://openresty.org/download/$pkgname-$pkgver.tar.gz{,.asc}
        lua-resty-extra-lib.tar.gz
        configure-add-lua-packages.patch
        service
        logrotate
        git+https://github.com/openssl/openssl#tag=OpenSSL_1_1_0e)
noextract=()
validpgpkeys=('25451EB088460026195BD62CB550E09EA0E98066')
sha256sums=('7f9ca62cfa1e4aedf29df9169aed0395fd1b90de254139996e554367db4d5a01'
            'SKIP'
            'SKIP' # lua-resty-extra-lib.tar.gz is built at make time
	    '0897f04569a4588d8e78b080c4adad597cb47c489c4ffde046ee6aaac6ba8acb'
            '7230ec16915bee37dda6d0928a533e6404c8cbe3fd778a88e0fb75530209a7dc'
            '2613986dd5faab09ca962264f16841c8c55c3a0bc7a5bb737eabd83143090878'
            'SKIP')
backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/nginx)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # patch configure to include lua_packages.tar.gz packages
  patch -Np1 -i "$srcdir"/configure-add-lua-packages.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Prologin-SADM
  ./configure \
    --prefix=${_cfgdir} \
    --conf-path=${_cfgdir}/nginx.conf \
    --sbin-path=/usr/bin/nginx \
    --pid-path=/run/nginx.pid \
    --lock-path=/run/lock/nginx.lock \
    --user=http \
    --group=http \
    --http-log-path=/var/log/nginx/access.log \
    --error-log-path=stderr \
    --http-client-body-temp-path=/var/lib/nginx/client-body \
    --http-proxy-temp-path=/var/lib/nginx/proxy \
    --http-fastcgi-temp-path=/var/lib/nginx/fastcgi \
    --http-scgi-temp-path=/var/lib/nginx/scgi \
    --http-uwsgi-temp-path=/var/lib/nginx/uwsgi \
    --with-file-aio \
    --with-http_addition_module \
    --with-http_dav_module \
    --with-http_degradation_module \
    --with-http_flv_module \
    --with-http_gunzip_module \
    --with-http_gzip_static_module \
    --with-http_iconv_module \
    --with-http_mp4_module \
    --with-http_realip_module \
    --with-http_secure_link_module \
    --with-http_stub_status_module \
    --with-http_sub_module \
    --with-http_v2_module \
    --with-imap \
    --with-ipv6 \
    --with-luajit \
    --with-pcre-jit \
    --without-http_ssl_module \
    --with-stream \
    --with-openssl=${srcdir}/openssl

  make -j$(nproc)
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/nginx/*.default

  install -d "$pkgdir"/var/lib/nginx
  install -dm700 "$pkgdir"/var/lib/nginx/proxy

  chmod 750 "$pkgdir"/var/log/nginx
  chown http:log "$pkgdir"/var/log/nginx

  install -d "$pkgdir"/usr/share/nginx
  mv "$pkgdir"/etc/nginx/nginx/html/ "$pkgdir"/usr/share/nginx

  install -d "$pkgdir"/etc/logrotate.d
  install -m644 "$srcdir/logrotate" "$pkgdir"/etc/logrotate.d/nginx

  install -Dm644 "$srcdir/service" "$pkgdir"/usr/lib/systemd/system/nginx.service

  rmdir "$pkgdir/run"
}

# vim:set ts=2 sw=2 et:
